import com.netflix.spinnaker.gradle.publishing.bintray.BintrayPublishPlugin

/*
 * Copyright 2017 Google, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'nebula.ospackage'
project.tasks.register("publish")
apply plugin: BintrayPublishPlugin

ospackage() {
  packageName = project.name

  // common installation paths 
  def spinnakerMonitoringHome = "/opt/spinnaker-monitoring"
  
  // global package requirements
  version = toVers(project.version.toString())
  release = '3'
  
  requires('python-pip')

  // As a rule of thumb we are going to promote this package content
  // into the parent /opt/spinnaker-monitoring directory.
  // Not in a "daemon" module.

  // installer scripts
  postInstall = file('pkg_scripts/postInstall.sh')
  preInstall = file('pkg_scripts/preInstall.sh')
  
  // python source code in subdirectory with the name
  from('spinnaker-monitoring') {
    into('/opt/spinnaker-monitoring/spinnaker-monitoring')
  }

  from(file('requirements.txt')) {
    into('/opt/spinnaker-monitoring')
  }
  
  from(file('config/spinnaker-monitoring.yml')) {
    into('/opt/spinnaker-monitoring/config')
  }

  from('bin') {
    into('/opt/spinnaker-monitoring/bin')
    fileMode  0755
  }  
  
  from('registry.dev') {
    into('/opt/spinnaker-monitoring/registry.example')
    fileMode  0600
  }  
  
  from(file('etc/init/spinnaker-monitoring.conf')) {
    into('/etc/init')
    user  'root'
    permissionGroup  'root'
    fileType  CONFIG | NOREPLACE
  }
}

task test(type: Exec) {
  workingDir "$projectDir"
  commandLine './tests/run_tests.sh'
}

tasks.build.dependsOn('test')

defaultTasks ':test'
